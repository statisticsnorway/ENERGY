{Application 'LISTING' logic file generated by CSPro}
PROC GLOBAL

	string currNorth, currEast;
	string region, district, subdistrict, locality, village, ea, serial, urbRur;
	numeric gpsN, gpsE;
	string country;	//Need to know the country to find the right map..
	map eaMap;	//To display the ea borders
	
	
	//function to strip leading spaces (the original strip function only strips trailing spaces)
	function string lstrip(string theString)
		numeric aPos = pos(" ", theString);
		while aPos = 1 do
			//leading space
			theString = theString[2];
			aPos = pos(" ", theString);
		enddo;
		lstrip = theString;
	end;
	
	
	
	
	//As I don't want to enter the same variables again and again for the same EA,
	//it's saved in this function..
	function savesettings()
		//Need to store the variables to next questionnaire
		saveSetting("Slogin", LLOGIN);
		saveSetting("Sregion", edit("ZZ", visualValue(LL1)));
		saveSetting("Sdistrict", edit("ZZ", visualValue(LL2)));
		saveSetting("SsubDistrict", edit("ZZZ", visualValue(LL3)));
		saveSetting("Slocality", edit("ZZ", visualValue(LL4)));
		saveSetting("SVillage", edit("ZZ", visualValue(LL5)));
		saveSetting("Sea", edit("ZZZ", visualValue(LL6)));
		string tmp = edit("ZZZ", visualValue(LL8));

		saveSetting("Sserial", tmp);
		saveSetting("ScurrNorth", edit("ZZZ.ZZZZZZ", visualValue(LL7A)));
		saveSetting("ScurrEast", edit("ZZZ.ZZZZZZ", visualValue(LL7B)));
		saveSetting("SurbanRural", edit("Z", visualValue(LLUR)));
		saveSetting("Country", country);
	end;


	function stopApp()
		saveSettings();
		stop(1);
	end;
	
	//For the user bar: Displays the map of the ea
	function showEA()
		string mapFileName = "../Maps/" + country + "_" + edit("99", toNumber(region))
				+ edit("99", toNumber(district)) + edit("999", toNumber(subdistrict))
				+ edit("99", toNumber(locality)) + edit("99", toNumber(village))
				+ edit("999", toNumber(ea)) + ".mbtiles";
		eaMap.setBaseMap(mapFileName);
		eaMap.show();
	end;
	
		
		
PROC LISTING_FF
preproc
	userbar(clear);
	userbar(add button, makeText(tr(102)), stopApp());
	userbar(add button, makeText(tr(110)), showEA());
	userbar(show);


PROC LISTING_QUEST_FORM
preproc
	LLOGIN = loadSetting("Slogin");	
	region = loadSetting("Sregion");
	district = loadSetting("Sdistrict");
	subdistrict = loadSetting("SsubDistrict");
	locality = loadSetting("Slocality");
	village = loadSetting("SVillage");
	ea = loadSetting("Sea");
	serial = loadSetting("Sserial");
	urbRur = loadSetting("SurbanRural");
	country = loadSetting("Country");
	
	//errmsg("%s=%s,%s=%s,%s=%s,%s=%s,%s=%s,%s=%s,%s=%s,%s=%s",
	//		LLogin, sysparm("LOGIN"), region, sysparm("REGION"), district, sysparm("DISTRICT"),
	// 		subdistrict, sysparm("SUBDISTRICT"), locality, sysparm("LOCALITY"), village,
	//		sysparm("VILLAGE"), ea, sysparm("EA"), urbRur, sysparm("URBANRURAL"));
		
	
	if lstrip(LLOGIN) = lstrip(sysparm("LOGIN")) and
				lstrip(region) = lstrip(sysparm("REGION")) and	
				lstrip(district) = lstrip(sysparm("DISTRICT")) and
				lstrip(subdistrict) = lstrip(sysparm("SUBDISTRICT")) and
				lStrip(locality) = lstrip(sysparm("LOCALITY")) and
				lstrip(village) = lstrip(sysparm("VILLAGE")) and
				lstrip(ea) = lstrip(sysparm("EA")) and
				lstrip(urbRUr) = lstrip(sysparm("URBANRURAL")) then
		
		//Still in the same ea. For some reason it doesn't quite work, so choosing highest serial to avoid duplicates
		if lstrip(sysparm("SERIAL")) <> "NOT" then
			if toNumber(lstrip(sysparm("SERIAL"))) > toNumber(serial) then
				LL8 = toNumber(lstrip(sysparm("SERIAL"))) + 1;
			else
				LL8 = toNumber(serial) + 1;
			endif;
		else
			LL8 = toNumber(serial) + 1;
		endif;
		
	else
		//new ea
		savesetting(clear);
		LLOGIN = sysparm("LOGIN");
		region = sysparm("REGION");
		district = sysparm("DISTRICT");
		subdistrict = sysparm("SUBDISTRICT");
		locality = sysparm("LOCALITY");
		village = sysparm("VILLAGE");
		ea = sysparm("EA");
		serial = sysparm("SERIAL");
		LL8 = toNumber(serial);
		urbRur = sysparm("URBANRURAL");
		country = sysparm("country");
	endif;

	LL1 = toNumber(region);
	LL2 = toNumber(district);
	LL3 = toNumber(subdistrict);
	LL4 = toNumber(locality);
	LL5 = toNumber(village);
	LL6 = toNumber(ea);
	LLUR = toNumber(urbRur);
	
	


postproc 
	savesettings();	//Saving the current settings for the next listing


PROC LL7

	if $ = 1 then
		gps(open); // on Android
		//gps(open,3,4800); // on a laptop or Windows tablet; COM3, 4800baud

		if gps(read,60) then // a successful attempt at a read, for up to 60 seconds
		    //errmsg("Latitude is %f, longitude is %f",gps(latitude),gps(longitude));
		    gpsN = gps(latitude);
		    gpsE = gps(longitude);		    
		else
		    errmsg(maketext(tr(105)))
		    	select(maketext(tr(103)), LL7, makeText(tr(104)), LL9);
			reenter;
		endif;
		gps(close);

	else //Get from previous
		numeric gpsOk = 1;
		if loadsetting("ScurrNorth") <> "NOTAPPL" and loadsetting("ScurrNorth") <> "" then
			gpsN = toNumber(loadSetting("ScurrNorth"));
		else
			gpsOk = 0;
		endif;
		if loadsetting("ScurrEast") <> "NOTAPPL" and loadSetting("ScurrEast") <> "" then
			gpsE = toNumber(loadSetting("ScurrEast"));
		else
			gpsOk = 0;
		endif;
		if gpsOk = 0 then
			errmsg(tr(101));
			$ = NOTAPPL;
			reenter;
		endif;
		
	endif;
	
	
PROC LL7A
preproc
	$ = gpsN;
	noInput;



PROC LL7B
preproc
	$ = gpsE;
	noinput;



PROC LL9
	if $ <> 1 and $ <> 2 then
		//Ending the listing. Nobody here. 
		savesettings();
		endlevel;
	endif;
	
	
	
PROC LL10
	if $ = "" then
		errmsg(tr(106))
				select(maketext(tr(107)), reenter, makeText(tr(108)), LL9);
	endif;
	
	
	
PROC LL11
	if $ = "" then
		errmsg(tr(109));
		reenter;
	endif;
