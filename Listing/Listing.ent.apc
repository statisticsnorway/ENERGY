{Application 'LISTING' logic file generated by CSPro}
PROC GLOBAL

	string currNorth, currEast;
	string state, county, subcounty, village, ea, serial;
	numeric gpsN, gpsE;
	
	
	//function to strip leading spaces (the original strip function only strips trailing spaces)
	function string lstrip(string theString)
		numeric aPos = pos(" ", theString);
		while aPos = 1 do
			//leading space
			theString = theString[2];
			aPos = pos(" ", theString);
		enddo;
		lstrip = theString;
	end;
	
	
	function stopApp()
		stop(1);
	end;
	
	
	//As I don't want to enter the same variables again and again for the same EA,
	//it's saved in this function..
	function savesettings()
		//Need to store the variables to next questionnaire
		saveSetting("SdeviceId", LDEVICE_ID);
		saveSetting("Sstate", edit("ZZZ", LL1));
		saveSetting("Scounty", edit("ZZZ",LL2));
		saveSetting("SsubCounty", edit("ZZZ", LL3));
		saveSetting("SVillage", edit("ZZZ", LL3_1));
		saveSetting("Sea", edit("ZZZ", LL4));
		string tmp = edit("ZZZ", LL8);

		saveSetting("Sserial", tmp);
		saveSetting("ScurrNorth", edit("ZZZ.ZZZZZZ", visualValue(LL6)));
		saveSetting("ScurrEast", edit("ZZZ.ZZZZZZ", visualValue(LL7)));
	end;


	


PROC LISTING_FF
preproc
	userbar(clear);
	userbar(add button, makeText(tr(102)), stopApp());
	userbar(show);



PROC LISTING_QUEST



PROC LISTING_QUEST_FORM
preproc
	LDEVICE_ID = loadSetting("SdeviceId");	//Haven't quite decided about what to do with the deviceId yet
	state = loadSetting("Sstate");
	county = loadSetting("Scounty");
	subCounty = loadSetting("SsubCounty");
	village = loadSetting("SVillage");
	ea = loadSetting("Sea");
	serial = loadSetting("Sserial");
	
	if lstrip(LDEVICE_ID) = lstrip(sysparm("DEVICE")) and
				lstrip(state) = lstrip(sysparm("STATE")) and	
				lstrip(county) = lstrip(sysparm("COUNTY")) and
				lstrip(subcounty) = lstrip(sysparm("SUBCOUNTY")) and
				lstrip(village) = lstrip(sysparm("VILLAGE")) and
				lstrip(ea) = lstrip(sysparm("EA")) then
		
		//Still in the same ea
		serial = loadSetting("Sserial");
		//serial = max(LL8 where LL1 = state);
		LL8 = toNumber(serial) + 1;
	else
		//new ea
		LDEVICE_ID = sysparm("DEVICE");
		state = sysparm("STATE");
		county = sysparm("COUNTY");
		subcounty = sysparm("SUBCOUNTY");
		village = sysparm("VILLAGE");
		ea = sysparm("EA");
		serial = "1";
		LL8 = 1;
	endif;

	LL1 = toNumber(state);
	LL2 = toNumber(county);
	LL3 = toNumber(subcounty);
	LL3_1 = toNumber(village);
	LL4 = toNumber(ea);
	
	


postproc 
	savesettings();	//Saving the current settings for the next listing


PROC LL6A
	if $ = 1 then
		gps(open); // on Android
		//gps(open,3,4800); // on a laptop or Windows tablet; COM3, 4800baud

		if gps(read,60) then // a successful attempt at a read, for up to 60 seconds
		    //errmsg("Latitude is %f, longitude is %f",gps(latitude),gps(longitude));
		    gpsN = gps(latitude);
		    gpsE = gps(longitude);		    
		else
		    errmsg(maketext(tr(105)))
		    	select(maketext(tr(103)), LL6A, makeText(tr(104)), LL5);
			reenter;
		endif;
		gps(close);

	else //Get from previous
		numeric gpsOk = 1;
		if loadsetting("ScurrNorth") <> "NOTAPPL" and loadsetting("ScurrNorth") <> "" then
			gpsN = toNumber(loadSetting("ScurrNorth"));
		else
			gpsOk = 0;
		endif;
		if loadsetting("ScurrEast") <> "NOTAPPL" and loadSetting("ScurrEast") <> "" then
			gpsE = toNumber(loadSetting("ScurrEast"));
		else
			gpsOk = 0;
		endif;
		if gpsOk = 0 then
			errmsg(tr(101));
			$ = NOTAPPL;
			reenter;
		endif;
		
	endif;

		
		
PROC LL6
preproc
	$ = gpsN;
	noInput;
PROC LL7
preproc
	$ = gpsE;
	noinput;
PROC LL11
	if $ = 3 then
		//Ending the listing. Nobody here. 
		savesettings();
		endlevel;
	endif;
	
	
	
